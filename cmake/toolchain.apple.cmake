if(TARGET_PLATFORM MATCHES "^host-darwin-|^ios-|^tvos-|^catalyst-")
  include(${CMAKE_CURRENT_LIST_DIR}/versions.apple.cmake)

  unset(PLATFORM)
  unset(MACOS_X64)
  unset(MACOS_ARM64)
  if(TARGET_PLATFORM STREQUAL host-darwin-x64)
    set(PLATFORM "MACOS_X64")
    set(MACOS_X64 True)
  elseif(TARGET_PLATFORM STREQUAL host-darwin-arm64)
    set(PLATFORM "MACOS_ARM64")
    set(MACOS_ARM64 True)
  elseif(TARGET_PLATFORM STREQUAL ios-simx64)
    set(PLATFORM "SIMULATOR64")
  elseif(TARGET_PLATFORM STREQUAL ios-simarm64)
    set(PLATFORM "SIMULATORARM64")
  elseif(TARGET_PLATFORM STREQUAL tvos-simx64)
    set(PLATFORM "SIMULATOR_TVOS")
  elseif(TARGET_PLATFORM MATCHES "^ios-arm")
    set(PLATFORM "OS")
    if(TARGET_PLATFORM STREQUAL ios-armv7)
      set(ARCHS "armv7")
    elseif(TARGET_PLATFORM STREQUAL ios-armv7s)
      set(ARCHS "armv7s")
    elseif(TARGET_PLATFORM STREQUAL ios-arm64)
      set(ARCHS "arm64")
    else()
      message(FATAL_ERROR "Unrecognized iOS target '${TARGET_PLATFORM}'")
    endif()
  elseif(TARGET_PLATFORM MATCHES "^tvos")
    set(PLATFORM "TVOS")
  elseif(TARGET_PLATFORM MATCHES "^catalyst-x64")
    set(PLATFORM "MAC_CATALYST")
  elseif(TARGET_PLATFORM MATCHES "^catalyst-arm64")
    set(PLATFORM "MAC_CATALYST_ARM64")
  endif()

  if(NOT PLATFORM)
    message(FATAL_ERROR "Target platform '${TARGET_PLATFORM}' unrecognized")
  endif()

  set(INCLUDE_IOS_TOOLCHAIN True)
  if(PLATFORM MATCHES "^OS|^SIMULATOR")
    set(DEPLOYMENT_TARGET ${IOS_MIN_VERSION})
  elseif(PLATFORM MATCHES "^MAC_CATALYST")
    set(DEPLOYMENT_TARGET ${MACOS_CATALYST_MIN_VERSION})
  elseif(PLATFORM MATCHES "^TVOS$|^SIMULATOR_TVOS$")
    set(DEPLOYMENT_TARGET ${TVOS_MIN_VERSION})
  elseif(PLATFORM MATCHES "^MACOS_")
    set(INCLUDE_IOS_TOOLCHAIN False)
  else()
    message(FATAL_ERROR "Platform '${PLATFORM}' not supported")
  endif()

  set(ENABLE_BITCODE OFF)
  set(ENABLE_ARC OFF)

  if (INCLUDE_IOS_TOOLCHAIN)
    include(${CMAKE_CURRENT_LIST_DIR}/../external/ios-cmake/ios.toolchain.cmake)
  endif()
endif()
